plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.alibaba.arouter' // Gradle 插件实现路由表的自动加载
    id 'dagger.hilt.android.plugin' // 依赖注入
}

android {
    compileSdkVersion skdVersion.compileSdkVersion //编译时使用 Android版本
    buildToolsVersion = skdVersion.buildToolsVersion //编译时使用的构建工具的版本

    defaultConfig {
//        applicationId "com.base.module.common"
        targetSdkVersion skdVersion.targetSdkVersion //目标版本
        minSdkVersion skdVersion.minSdkVersion //最低兼容Android版本

        versionCode libVersion.commonCode
        versionName libVersion.commonName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    // 路由需要
    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }
    buildTypes {
        release {
            consumerProguardFiles file('.').listFiles(new FilenameFilter() {
                @Override
                boolean accept(File file, String s) {
                    return s.endsWith('.pro')
                }
            })
        }
        debug {
            consumerProguardFiles file('.').listFiles(new FilenameFilter() {
                @Override
                boolean accept(File file, String s) {
                    return s.endsWith('.pro')
                }
            })
        }
    }

    // 启用 viewBinding
    buildFeatures {
        viewBinding = true
    }
    lintOptions {
        // 发现错误,是否停止构建
        abortOnError true
    }
    compileOptions {
        encoding "utf-8"
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
        useIR = true
    }
}

dependencies {
    def dependencies = rootProject.ext.dependencies

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    if (useLocalDependencies()) {
        implementation(project(':library'))
    } else {
        implementation dependencies.BaseLibrary
    }

    // Kotlin中使用了Glide注解
    kapt dependencies.glideCompiler
    // 路由
    kapt dependencies.arouterCompiler
    // 依赖注入
    implementation dependencies.hilt
    kapt dependencies.hiltCompiler

}